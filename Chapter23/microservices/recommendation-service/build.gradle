plugins {
    id 'org.springframework.boot' version '2.7.3'
    id 'io.spring.dependency-management' version '1.0.13.RELEASE'
    id 'java'
    id 'org.springframework.experimental.aot' version '0.12.1'
}

group = 'se.magnus.microservices.core.recommendation'
version = '1.0.0-SNAPSHOT'
sourceCompatibility = '1.8'

ext {
    mapstructVersion = "1.3.1.Final"
    springCloudVersion = "2021.0.3"
}

repositories {
    maven { url 'https://repo.spring.io/release' }
    mavenCentral()
}

// Prevents GraalVM to native compile locally, i.e. without Docker-buildpacks
//jar {
//    enabled = false
//}

dependencies {
    implementation project(':api')
    implementation project(':util')
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    implementation 'org.springframework.boot:spring-boot-starter-data-mongodb-reactive'
    // implementation 'org.springframework.cloud:spring-cloud-starter-stream-rabbit'
    implementation 'org.springframework.cloud:spring-cloud-starter-stream-kafka'
    // implementation 'org.springframework.cloud:spring-cloud-starter-sleuth'
    implementation "org.mapstruct:mapstruct:${mapstructVersion}"
    implementation 'io.micrometer:micrometer-registry-prometheus'

    // See https://github.com/netty/netty/issues/11693
    implementation group: 'io.netty', name: 'netty-resolver-dns-native-macos', classifier: 'osx-aarch_64'

    compileOnly "org.mapstruct:mapstruct-processor:${mapstructVersion}"
    annotationProcessor "org.mapstruct:mapstruct-processor:${mapstructVersion}"
    testAnnotationProcessor "org.mapstruct:mapstruct-processor:${mapstructVersion}"

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'io.projectreactor:reactor-test'

    implementation platform('org.testcontainers:testcontainers-bom:1.16.2')
    testImplementation 'org.testcontainers:testcontainers'
    testImplementation 'org.testcontainers:junit-jupiter'
    testImplementation 'org.testcontainers:mongodb'
    testImplementation 'org.testcontainers:kafka'
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

test {
    useJUnitPlatform()
    // Tracing agent works with the recommendation service, but is disabled to avoid changes to the source code, i.e. src/main/resources/META-INF/native-image
    // jvmArgs "-agentlib:native-image-agent=access-filter-file=src/test/resources/access-filter.json,config-output-dir=src/main/resources/META-INF/native-image"
}

bootBuildImage {
    builder = "paketobuildpacks/builder:tiny"
    imageName = "hands-on/native-recommendation-service"
    environment = [
        "BP_NATIVE_IMAGE" : "true",
        "BP_JVM_VERSION" : "17",
        "BP_NATIVE_IMAGE_BUILD_ARGUMENTS" : "--enable-url-protocols=http,https --initialize-at-build-time=sun.instrument.InstrumentationImpl"
    ]
}